Import('env', 'plugins')

filmEnv = env.Clone()
env_dict = filmEnv.Dictionary()
env_keys = env_dict.keys()
def has_key(key):
        return key in env_keys

if has_key('OEXRLIBDIR'):
        filmEnv.Prepend(LIBPATH=env['OEXRLIBDIR'])
if has_key('OEXRINCLUDE'):
        filmEnv.Prepend(CPPPATH=env['OEXRINCLUDE'])
if has_key('OEXRFLAGS'):
        filmEnv.Prepend(CPPFLAGS=env['OEXRFLAGS'])
if has_key('OEXRLIB'):
        filmEnv.Prepend(LIBS=env['OEXRLIB'])

plugins += filmEnv.SharedLibrary('mfilm', ['mfilm.cpp', 'cnpy.cpp'])
plugins += filmEnv.SharedLibrary('ldrfilm', ['ldrfilm.cpp'])
plugins += filmEnv.SharedLibrary('hdrfilm', ['hdrfilm.cpp'])

if ['MTS_HAS_OPENEXR', 1] in filmEnv['CPPDEFINES']:
        plugins += filmEnv.SharedLibrary('tiledhdrfilm', ['tiledhdrfilm.cpp'])

Export('plugins')
